# ### Approval Q&A system by Roy Curtis
# ### Licensed under MIT, 2015

# ### Global variables
# approval.await.%player% - boolean; if a player is awaiting approval
# approval.wrong.%player% - number; how many wrong answers by player

# ### Configuration

options:
  max wrong answers    : 5
  wrong answer message : [AUTOMATIC] Too many wrong answers; gamealition@gmail.com

# ### Events

on player join:
    player has permission "gamealition.newbie"
    message "Sending approval messages to %player%" to console
    set {approval.pickup.%player%} to now

    wait 10 seconds
    message "&2&LHello, %player%! &fYou can look around freely."
    message " To join our community, do the command &3/approve-me"

on pick up:
    player has permission "gamealition.newbie"
    cancel the event
    # Do message only every 6 seconds
    {approval.pickup.%player%} was more than 6 seconds ago
    set {approval.pickup.%player%} to now
    execute console command "/guest-error %player% Can't pick that up!"

on hunger level change:
    player has permission "gamealition.newbie"
    set player's hunger level to 10

on break:
    player has permission "gamealition.newbie"
    cancel the event
    execute console command "/guest-error %player% Can't break that!"
    
on craft:
    player has permission "gamealition.newbie"
    cancel the event
    execute console command "/guest-error %player% Can't craft that!"

on place:
    player has permission "gamealition.newbie"
    cancel the event
    execute console command "/guest-error %player% Can't place that!"
    
on right click on a chest or a hopper or a trapped chest or a trapdoor:
    player has permission "gamealition.newbie"
    cancel the event
    execute console command "/guest-error %player% Can't open that!"

on vehicle destroy:
    player has permission "gamealition.newbie"
    cancel the event
    execute console command "/guest-error %player% Can't destroy that!"

on damage:
    attacker is a player
    attacker has permission "gamealition.newbie"
    cancel the event
    execute console command "/guest-error %attacker% Can't hurt that!"

# ### Newbie island functions

command /ni-wrong <text> <player>:
    executable by: console
    trigger:
        # Logic
        set {_dest} to arg 1
        set {_player} to arg 2
        execute console command "/mvtp %{_player}% %{_dest}%"
        message "&c*** Wrong answer! Please try again" to {_player}

        {_player} has permission "gamealition.newbie"
        add 1 to {approval.wrong.%{_player}%}

        if {approval.wrong.%{_player}%} >= {@max wrong answers}:
            delete {approval.wrong.%{_player}%}
            execute console command "/ban %{_player}% {@wrong answer message}"
        else:
            message "*** Wrong newbie island answer by %{_player}%, total %{approval.wrong.%{_player}%}%" to console

# ### Approval functions

command /approve <offline player>:
    description: Adds given player to the "player" perm group
    permission: gamealition.moderator
    executable by: players and console
    trigger:
        if arg 1 has the permission "derpi.inmate":
            message "&c*** %arg 1%r is in the DERPI; they will be approved when released"
            exit trigger

        if arg 1 has the permission "gamealition.player":
            message "&c*** %arg 1% is already approved"
            exit trigger

        delete {approval.await.%arg 1%}
        delete {approval.wrong.%arg 1%}
        execute console command "/perms player %arg 1% setgroup player"
        execute console command "/perms player %arg 1% set gamealition.flatland false"
        execute console command "/say &f%arg 1% &7has been &aapproved&7 for server play"
        
command /unapprove <offline player>:
    description: Removes given player from the "player" perm group
    permission: gamealition.moderator
    executable by: players and console
    trigger:
        if arg 1 has the permission "derpi.inmate":
            message "&c*** %arg 1% is in the DERPI; unapprove when released"
            exit trigger

        delete {approval.await.%arg 1%}
        delete {approval.wrong.%arg 1%}
        execute console command "/perms player %arg 1% setgroup default"
        execute console command "/say &f%arg 1% &7has been &cunapproved&7 for server play"

command /approve-me:
    description: Allows a new (unapproved) player to go through the approval process
    permission: gamealition.newbie
    executable by: players
    trigger:
        if {approval.await.%player%} is set:
            message "&f*** &3You've already gone through approval! &fPlease wait for staff to approve you"
            exit trigger

        execute console command "/mvtp %player% a:newbie_part1"
        # Wait for region messages
        wait 1 second
        message "&f*** Please follow the lights and read the signs carefully"

command /approve-manual <player>:
    description: Notify staff that a player is awaiting approval
    executable by: console
    trigger:
        arg 1 has permission "gamealition.newbie"

        if {approval.await.%arg 1%} is set:
            message "&f*** &3You've already gone through approval! &fPlease wait for staff to approve you" to arg 1
            exit trigger
        else:
            set {approval.await.%arg 1%} to true
            delete {approval.wrong.%arg 1%}
            message "&f*** &3Thanks for reading our rules! &fYou will need to wait for staff to approve you manually. Feel free to explore in the meantime!" to arg 1
            execute console command "/perms player %arg 1% set ticketmaster.user.basic"
            execute arg 1 command "/modreq Player %arg 1% has gone through newbie island and would like approval. Use `/approve %arg 1%` (IRC whilst opped: `.approve %arg 1%`) to approve at your own discretion"
            execute console command "/perms player %arg 1% unset ticketmaster.user.basic"

command /guest-error <player> <text=Can't do that!>:
    description: Gives newbie error to guests who try to do restricted things
    executable by: console
    trigger:
        message "&2&lYou can look but not touch, %arg 1%!" to arg 1
        message "&fDo the command &3/approve-me &fto begin the approval process!" to arg 1
        execute console command "/title %arg 1% times 10 60 40"
        execute console command "/title %arg 1% subtitle [""Want to join? Do "",{color:dark_aqua,text:""/approve-me!""}]"
        execute console command "/title %arg 1% title {color:red,text:""%arg 2%""}"
        message "Sent approval message to %arg 1%" to console