# ### Build-a-thon administration by Roy Curtis
# ### Licensed under MIT, 2016

# Commands for general management of Build-a-thon events

# ### Permission nodes
# gamealition.buildathon.host - Player is a build-a-thon host

# ### Global variables
# buildathon.host - player; current build-a-thon host

# ### Global functions

function batRemoveHost(_: number = 0):
    {buildathon.host} is set
    console command "/perms group buildathon-host remove %{buildathon.host}%"
    message "*** You are no longer the Build-a-thon host" to {buildathon.host}
    delete {buildathon.host}

function batSetHost(who: player):
    set {buildathon.host} to {_who}
    console command "/perms group buildathon-host add %{_who}%"
    message "*** You are now the Build-a-thon host!" to {_who}

function batIsHost(who: player) :: boolean:
    if {_who} has permission "gamealition.buildathon.host":
        return true
    else if isStaff({_who}) is true:
        return true
    else:
        return false

# ### Global commands

command /buildathon-host [<offline player>]:
    description:   Gets or sets the current Build-a-thon host
    executable by: players and console
    trigger:
        # Getter
        if arg 1 is not set:
            message "*** Current Build-a-thon host is %name({buildathon.host})%"
            exit trigger

        # Setter
        if isStaff(player) is true:
            batRemoveHost()
            batSetHost(arg 1)
            message "*** %name(arg 1)% is now the Build-a-thon host"
        else:
            message "&c*** You must be staff to set the BAT host"

command /buildathon-clearhost:
    description:   Clears the current Build-a-thon host
    executable by: players and console
    permission:    gamealition.mod-high
    trigger:
        message "*** Removed %name({buildathon.host})% as Build-a-thon host"
        batRemoveHost()

command /buildathon-ticket <player>:
    description:   Handles Build-a-thon ticket purchase
    executable by: players and console
    permission:    gamealition.mod-high
    permission:    gamealition.buildathon.host
    trigger:
        set {_target} to arg 1
        
        console command "/perms player %{_target}% addgroup buildathon"
        console command "/playsound entity.player.levelup neutral %{_target}% ~ ~ ~ 1 1 1"
        message "&2*** You can now build in the Build-a-thon lots!" to {_target}
        message "* Be sure to read the signs around you for information!" to {_target}
        message "* You can also do &3/motd &2to get information!" to {_target}
        message "*** %{_target}% given Build-a-thon ticket"

command /buildathon-reset:
    description:   Clears all players from Build-a-thon region rights
    executable by: players and console
    permission:    gamealition.mod-high
    permission:    gamealition.buildathon.host
    trigger:
        console command "/perms group buildathon purge -m"
        message "*** Build-a-thon region has been cleared of players"