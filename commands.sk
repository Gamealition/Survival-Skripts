# Commands and aliases

command /spawn:
    description: Takes you to the world's spawn
    aliases: /hub, /lobby
    permission: multiverse.core.spawn.self
    executable by: players
    trigger:
        execute console command "/mv tp %player% world"
        
command //replacestair <text> <text>:
    description: Assists with replacing stairs with the correct orientation
    aliases: //replacestairs
    permission: worldedit.region.replace
    executable by: players
    trigger:
        execute player command "//replace %arg 1%:0 %arg 2%:0"
        execute player command "//replace %arg 1%:1 %arg 2%:1"
        execute player command "//replace %arg 1%:2 %arg 2%:2"
        execute player command "//replace %arg 1%:3 %arg 2%:3"
        execute player command "//replace %arg 1%:4 %arg 2%:4"
        execute player command "//replace %arg 1%:5 %arg 2%:5"
        execute player command "//replace %arg 1%:6 %arg 2%:6"
        execute player command "//replace %arg 1%:7 %arg 2%:7"
        
command /ride <player>:
    description: Allows the riding of a player
    permission: gamealition.moderator
    executable by: players
    trigger:
        if arg 1 is the player:
            message "&cYou cannot ride yourself!"
            exit trigger
        
        make the player ride arg 1
        
command /approve <offline player>:
    description: Adds given player to the "player" perm group
    permission: gamealition.moderator
    executable by: players and console
    trigger:
        execute console command "/perms group player add %arg 1%"
        message "&7*** Player &f%arg 1% &7has been &aapproved&7 for server play"
        
command /unapprove <offline player>:
    description: Removes given player from the "player" perm group
    permission: gamealition.moderator
    executable by: players and console
    trigger:
        execute console command "/perms group player remove %arg 1%"
        message "&7*** Player &f%arg 1% &7has been &cunapproved&7 for server play"