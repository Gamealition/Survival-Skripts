# ### General commands & aliases by Roy Curtis
# ### Licensed under MIT, 2014

#              uuuuuuuuuuuuuuuuuuuu               
#            u" uuuuuuuuuuuuuuuuuu "u            
#          u" u$$$$$$$$$$$$$$$$$$$$u "u          
#        u" u$$$$$$$$$$$$$$$$$$$$$$$$u "u        
#      u" u$$$$$$$$$$$$$$$$$$$$$$$$$$$$u "u      
#    u" u$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$u "u    
#  u" u$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$u "u  IT IS CRITICAL THAT ALL ADDITIONS,
#  $ $$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$ $  REMOVALS OR MODIFICATIONS TO COMMANDS
#  $ $$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$ $  IN THIS SKRIPT FILE ARE TESTED WITH A
#  $ $$$" ... "$...  ...$" ... "$$$  ... "$$$ $  PLAYER IN THE DERPI TO ENSURE NO NEW
#  $ $$$u `"$$$$$$$  $$$  $$$$$  $$  $$$  $$$ $  EXPLOITS OR HOLES ARE INTRODUCED.
#  $ $$$$$$uu "$$$$  $$$  $$$$$  $$  """ u$$$ $  
#  $ $$$""$$$  $$$$  $$$u "$$$" u$$  $$$$$$$$ $  PLEASE ALWAYS NOTIFY STAFF WHEN ANY
#  $ $$$$....,$$$$$..$$$$$....,$$$$..$$$$$$$$ $  CHANGE IS MADE IN THIS FILE.
#  $ $$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$ $  
#  "u "$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$" u"  PLEASE ALWAYS TEST ANY CHANGES MADE
#    "u "$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$" u"    IN THIS FILE.
#      "u "$$$$$$$$$$$$$$$$$$$$$$$$$$$$" u"      
#        "u "$$$$$$$$$$$$$$$$$$$$$$$$" u"        - Roy
#          "u "$$$$$$$$$$$$$$$$$$$$" u"          
#            "u """""""""""""""""" u"            
#              """"""""""""""""""""               

command /spawn:
    description: Takes you to the server's spawn
    aliases: /hub, /lobby
    permission: multiverse.core.spawn.self
    executable by: players
    trigger:
        player does not have permission "derpi.inmate"
        execute console command "/mv tp %player% a:world"
        
command /servertime:
    description: Get's the server's IRL time/date
    aliases: /stime, /date
    executable by: players and console
    trigger:
        message "&7*** Current server date/time: &f%now%"
        
command /links:
    description: Get's the server's IRL time/date
    aliases: /www
    executable by: players and console
    trigger:
        message "&B&LGamealition &f&lSurvival &fofficial links:"
        message "&f• &3gamealition.com &f- Official"
        message "&f• &3forums.gamealition.com &f- Forums"
        message "&f• &3irc.gamealition.com &f- IRC"
        message "&f• &3survival.gamealition.com/rules &f- Rules"
        message "&f• &3survival.gamealition.com/dynmap &f- Dynmap"
        message "&f• &3survival.gamealition.com/features &f- Features"
        message "&f• &3survival.gamealition.com/changelog &f- Changelog"
        message "&f• &3survival.gamealition.com/bugs &f- Known issues"

command /modreq <text>:
    description: Files a ticket for staff to handle at current location. Alias for "/ticket new"
    executable by: players
    permission: ticketmaster.user.basic
    trigger:
        make player execute command "/ticket new %arg 1%"

command /broadcast-irc <text>:
    description: Broadcasts a message to both in-game and IRC (needed due to Skript/PurpleIRC bugs)
    executable by: players and console
    permission: gamealition.mod-high
    trigger:
        broadcast colored arg 1
        console command "/irc send GamealitionMC ##Gamealition %colored arg 1%"

command /fixtoggle:
    aliases: /fixtoggles, /fixsign, /fixsigns, /fixtogglesigns, /reloadsignshop
    permission: gamealition.approved
    trigger:
        {fixtoggle.lastused} was less than a minute ago:
            message "&7*** This command has a minute cooldown. Try again later"
            stop
        set {fixtoggle.lastused} to now
        message "&7*** &fSignShop has been reloaded"
        execute console command "/signshop reload"
        
command //replacestair <text> <text>:
    description: Assists with replacing stairs with the correct orientation
    aliases: //replacestairs
    permission: worldedit.region.replace
    executable by: players
    trigger:
        execute player command "//replace %arg 1%:0 %arg 2%:0"
        execute player command "//replace %arg 1%:1 %arg 2%:1"
        execute player command "//replace %arg 1%:2 %arg 2%:2"
        execute player command "//replace %arg 1%:3 %arg 2%:3"
        execute player command "//replace %arg 1%:4 %arg 2%:4"
        execute player command "//replace %arg 1%:5 %arg 2%:5"
        execute player command "//replace %arg 1%:6 %arg 2%:6"
        execute player command "//replace %arg 1%:7 %arg 2%:7"

command /dmap-enable <text> <boolean>:
    description: Shortcut for /dmap worldset <worldname> enabled:<bool> 
    permission: dynmap.dmap.worldset
    executable by: players and console
    trigger:
        execute player command "/dynmap pause all"
        execute player command "/dmap worldset %arg 1% enabled:%arg 2%"
        execute player command "/dynmap pause none"

command /dynmap-here:
    description: Broadcasts a link to Dynmap zoomed to your location
    executable by: players
    trigger:
        set {_url} to "http://survival.gamealition.com/dynmap/%player's world%/%rounded player's x-coordinate%/%rounded player's y-coordinate%/%rounded player's z-coordinate%"
        console command "/broadcast-irc *** Dynmap URL for %player%'s location: &3%{_url}%"
        
command /ride <player> <player>:
    description: Allows the riding of a player
    permission: gamealition.mod-high
    executable by: players
    trigger:
        if arg 1 is the player:
            arg 2 is the player
            message "&c*** You cannot ride yourself!"
            exit trigger
        
        make arg 1 ride arg 2

command /gaybrush:
    description: Fabulous!
    executable by: players
    trigger:
        player command "//none"
        player command "//br cyl 35:0,35:1,35:2,35:3,35:4,35:5,35:6,35:7,35:8,35:9,35:10,35:11,35:12,35:13,35:14,35:15 10"

command /item-destroy:
    description: Destroy's currently held item
    permission: gamealition.mod-high
    executable by: players
    trigger:
        player is holding anything
        message "*** Destroyed %player's held item%"
        delete player's held item

command /item-damage <number>:
    description: Sets damage value of held item
    permission: gamealition.mod-high
    executable by: players
    trigger:
        player is holding anything
        set data value of player's held item to arg 1
        message "*** Set damage of %player's held item% to %arg 1%"

command /item-rename <text>:
    description: Renames held item to the given text
    permission: gamealition.mod-high
    executable by: players
    trigger:
        player is holding anything
        set name of player's held item to colored arg 1
        message "*** Renamed %player's held item% to ""%colored arg 1%&r"""

command /item-lore <text>:
    description: Sets lore tagline of held item
    permission: gamealition.mod-high
    executable by: players
    trigger:
        player is holding anything
        set 1st line of (player's held item)'s lore to colored arg 1
        message "*** Set 1st lore line of %player's held item% to ""%colored arg 1%&r"""

# Courtesy of http://heads.freshcoal.com/maincollection.php
command /tea <player=%player%>:
    permission: gamealition.mod-high
    executable by: players
    trigger:
        message "*** Tea, earl grey, hot" to arg 1
        execute console command "/give %arg 1% skull 1 3 {display:{Name:""Cup of Tea""},SkullOwner:{Name:""%arg 1%"",Id:""2a3545d4-3ea5-4d26-9033-c280476696e0"",Properties:{textures:[{Value:""eyJ0ZXh0dXJlcyI6eyJTS0lOIjp7InVybCI6Imh0dHA6Ly90ZXh0dXJlcy5taW5lY3JhZnQubmV0L3RleHR1cmUvZDhlOTRkZGQ3NjlhNWJlYTc0ODM3NmI0ZWM3MzgzZmQzNmQyNjc4OTRkN2MzYmVlMDExZThlNGY1ZmNkNyJ9fX0=""}]}}}"

# Courtesy of http://heads.freshcoal.com/maincollection.php
command /coffee <player=%player%>:
    permission: gamealition.mod-high
    executable by: players
    trigger:
        message "*** Coffee, black" to arg 1
        execute console command "/give %arg 1% skull 1 3 {display:{Name:""Cup of Coffee""},SkullOwner:{Name:""%arg 1%"",Id:""3c534489-c08b-4c4f-a5ec-9bd9934be7c4"",Properties:{textures:[{Value:""eyJ0ZXh0dXJlcyI6eyJTS0lOIjp7InVybCI6Imh0dHA6Ly90ZXh0dXJlcy5taW5lY3JhZnQubmV0L3RleHR1cmUvNDExNTExYmRkNTViY2I4MjgwM2M4MDM5ZjFjMTU1ZmQ0MzA2MjYzNmUyM2Q0ZDQ2YzRkNzYxYzA0ZDIyYzIifX19""}]}}}"

command /isbanned <offline player>:
    description: Checks if a player is banned
    executable by: players and console
    trigger:
        if arg 1 is banned:
            message "&7*** &f%arg 1%&7 is &cperma-banned"
            message "&7*** For technical reasons, I cannot say when, why and by who"
        else:
            message "&7*** &f%arg 1%&7 is not banned"

command /age <offline player>:
    description: Gets the server age of a player
    executable by: players and console
    trigger:
        message "&7*** &2%arg 1%&f has been on the server..."
        message "&7• UUID &f%{uuid.%arg 1%}%"
        message "&7• Since &f%{age.%arg 1%}%"
        message "&7• Since (UUID) &f%{age.%uuid of arg 1%}%"
        message "&7• For &f%difference between {age.%arg 1%} and now%"

command /clearhistory <player>:
    description: Clears internal Skript history for player
    permission: gamealition.mod-high
    executable by: players and console
    trigger:
        delete {age.%name of arg 1%}
        delete {age.%uuid of arg 1%}
        delete {uuid.%name of arg 1%}
        delete {uuid.%uuid of arg 1%}
        message "&7*** Cleared history for %arg 1%"

command /stoplag:
    aliases: /halt-activity, /stopfire, /allowfire
    executable by: players and console
    trigger:
        message "&8&o*** The command you have dialed is not in service, please try again later"

command /reload:
    aliases: /spigot:restart, /bukkit:reload
    executable by: players and console
    trigger:
        message "&8&o*** The command you have dialed is not in service, please try again later"

command /kit:
    executable by: players
    trigger:
        message "*** &cThere are no kits on this server! &fHowever, you can visit the Toolshed for free player-donated tools and armor. Just go south of spawn, or ask!"

command /rtp:
    executable by: players
    trigger:
        message "*** &cSorry, we don't do random teleports! &fJust go any direction from spawn, or ask for help finding land!"

command /wild:
    executable by: players
    trigger:
        message "*** &2Looking for the wilderness? &fJust go any direction from spawn, or ask for help finding land!"

command /nearby-entities [<number=120>]:
    executable by: players
    trigger:
        set {_radius} to arg 1

        message "*** Searching for entities in a %{_radius}% block radius:"

        loop all entities in radius {_radius} around the player:
            add 1 to {_nearresults::%loop-entity's type%}

        loop {_nearresults::*}:
            message "%loop-index%: %loop-value%"

command /entity-rundown [<number=120>]:
    executable by: players and console
    trigger:
        set {_radius} to arg 1

        message "*** Entities in %{_radius}% block radius around players:"

        loop all players:
            set {_runresults::%loop-player's name%} to amount of entities in radius {_radius} around loop-player

        loop {_runresults::*}:
            message "%loop-index%: %loop-value%"

command /arm-armorstands:
    description: Replaces all armor stands in inventory with ones that have arms
    executable by: players
    permission: gamealition.approved
    trigger:
        set {_stands} to number of armor stand in the player's inventory

        if {_stands} is 0:
            message "&7*** You do not have any armor stands to arm"
            exit trigger

        remove all armor stand from the player
        execute console command "/minecraft:give %player% minecraft:armor_stand %{_stands}% 0 {EntityTag:{ShowArms:1}}"
        message "&7*** Your armor stands have been replaced with armed ones"

# Wurst ForceOp catch
command /login <text>:
    description: Login to OP rights (forgot password? try gamealition1)
    executable by: players
    trigger:
        set {_password} to arg 1

        if {_password} is player's name or "password" or "passwort" or "password1" or "passwort1" or "password123" or "passwort123" or "pass" or "pw" or "pw1" or "pw123" or "hallo" or "Wurst" or "wurst" or "1234" or "12345" or "123456" or "1234567" or "12345678" or "123456789" or "login" or "register" or "test" or "sicher" or "me" or "penis" or "penis1" or "penis123" or "minecraft" or "minecraft1" or "minecraft123" or "mc" or "admin" or "server" or "yourmom" or "tester" or "account" or "creeper" or "gronkh" or "lol" or "auth" or "authme" or "qwerty" or "qwertz" or "ficken" or "ficken1" or "ficken123" or "fuck" or "fuckme" or "fuckyou":
            execute console command "/ban %player% [AUTO] Your Wurst is showing. gamealition@gmail.com"
        else:
            kick the player because "*taps with wand* you are now an OP, %player%"
