# DERPI system by VanDerProtofsky

# ########################################################################################
# # PUBLIC COMMANDS ######################################################################
# ########################################################################################
    
command /shack:
    description: Teleports a quarried player back to the shack
    usage: /shack
    permission: derpi.inmate
    trigger:
        player's world is "world_derpi"
        execute console command "/mv tp %player% world_derpi"
    
# ########################################################################################
    
command /quarry <offline player> [<number=0>] <text>:
    description: Casts a player to the DERPI quarry
    usage: /quarry <who> [<money balance offset>] <reason>
    permission: derpi.moderator
    trigger:
        set {_target} to arg 1
        set {_targetUUID} to uuid of arg 1
        set {_offsetMoney} to arg 2
        set {_reason} to arg 3

        # Force negative offset
        if {_offsetMoney} is more than 0:
            message "&c*** Please specify a negative amount of money to set or leave it at 0"
            exit trigger
        
        if {_target} is offline:
            set {derpi.queuequarry.%{_target}%} to {_reason}
            message "&7*** That player is offline. They will be quarried when they next join the server."
            exit trigger
        
        if {derpi.%{_targetUUID}%.quarried} is set:
            if {_target}'s world is not "world_derpi":
                message "&c*** Oops, that player is not in the quarry world. Fixing..."
                execute console command "/mv tp %{_target}% world_derpi"
            else:
                message "&c*** That player has already been quarried"
                make the player execute command "/quarryinfo %{_target}%"
            exit trigger

        message "*** Quarrying %{_target}%..."
        
        # Save metadata...
        if command sender is the console:
            set {derpi.%{_targetUUID}%.quarried.by} to "[Console]"
        else:
            set {derpi.%{_targetUUID}%.quarried.by} to "%command sender%"
            
        set {derpi.%{_targetUUID}%.quarried} to now
        set {derpi.%{_targetUUID}%.quarried.because} to {_reason}
        
        # Logging
        add "↳ &cCommited &7by &f%command sender% &7on &f%now%&7, with money set to &f%{_offsetMoney}% &7because &f%{_reason}%" to {derpi.%{_target}%.log::*}
        
        # Handle the target...
        execute console command "/perms player %{_target}% setgroup inmate"
        execute console command "/mv tp %{_target}% world_derpi"
        set {_target}'s money to {_offsetMoney}
        
        # Explain the basics to the target...
        broadcast "&l*** &f%{_target}% &7has been &c&lcommitted to DERPI &7because: &f%{_reason}%"
        
        send "&4&l*** DEMEANOR ERADICATION & REHAB ***" to {_target}
        send "&4&l***     PENITENTIARY INSTITUTE     ***" to {_target}
        send "&cYou have been sent here because you have broken the rules." to {_target}
        send " &cConsequently, you must earn back your freedom to play." to {_target}
        send " &cThis is done by mining enough cobblestone to buy freedom." to {_target}
        send " &cUse &f/shack &cto teleport to the shack." to {_target}
        send " &cUse &f/money &cto check your balance." to {_target}
        send " &cPvP is enabled and you will be unable to hear or send chat." to {_target}
        
        # Good luck.
        heal {_target}
        clear {_target}'s inventory
        give a wooden pickaxe to {_target}
        
# ########################################################################################

command /unquarry <offline player>:
    description: Removes a player from the DERPI quarry
    usage: /quarry <who>
    permission: derpi.moderator
    trigger:
        set {_target} to arg 1
        set {_targetUUID} to uuid of arg 1
        
        if {_target} is offline:
            if {derpi.queuequarry.%{_target}%} exists:
                delete {derpi.queuequarry.%{_target}%}
                message "&7*** There was a quarry commit queued for this name. They will no longer be quarried on next join."
            else:
                set {derpi.queuerelease.%{_target}%} to true
                message "&7*** That player is offline. They will be released when they next join the server."
            exit trigger
        
        if {derpi.%{_targetUUID}%.quarried} is not set:
            message "&c*** That player is not quarried"
            exit trigger
            
        add "↳ &aReleased &7by &f%command sender% &7on &f%now%" to {derpi.%{_target}%.log::*}
        message "*** Unquarrying %{_target}%..."
        
        delete {derpi.%{_targetUUID}%.quarried}
        delete {derpi.%{_targetUUID}%.quarried.by}
        delete {derpi.%{_targetUUID}%.quarried.because}
                    
        remove 270 from {_target}'s money 
        execute console command "/perms player %{_target}% setgroup default"
        execute console command "/mvtp %{_target}% a:newbie_part1"
        
        broadcast "&l*** &f%{_target}% &7has been &2&lreleased from DERPI!"
        wait 1 second
        send "&c*** As part of your release, you must go through the approval process again and accept the rules." to {_target}
        
# ########################################################################################

command /quarryinfo <offline player>:
    description: Gets DERPI information on a player
    usage: /quarryinfo <who>
    permission: derpi.moderator
    trigger:
        set {_target} to arg 1
        set {_targetUUID} to uuid of arg 1

        # Try to fetch UUID from global cache if target is offline
        if {_targetUUID} is not set:
            set {_targetUUID} to {uuid.%{_target}%}
        
        # Handle unresolvable offline targets
        if {_targetUUID} is not set:
            if {derpi.queuequarry.%{_target}%} exists:
                message "&7*** That player is offline. However, they are queued for quarrying when they next join."
            else if {derpi.queuerelease.%{_target}%} exists:
                message "&7*** That player is offline. However, they are queued for release when they next join the server."
            else:
                message "&7*** That player has no DERPI record"
            exit trigger
            
        if {derpi.%{_targetUUID}%.quarried} is set:
            message "&7*** DERPI report of: &f%{_target}%"
            message "&7• quarried by &f%{derpi.%{_targetUUID}%.quarried.by}%"
            message "&7• on &f%{derpi.%{_targetUUID}%.quarried}%"
            message "&7• because &f%{derpi.%{_targetUUID}%.quarried.because}%"
        else:
            message "&7*** That player is not quarried"
            
# ########################################################################################

command /quarrylog <offline player>:
    description: Gets DERPI logs on a player name (not UUID!)
    usage: /quarrylog <who>
    permission: derpi.moderator
    trigger:
        set {_target} to arg 1
        
        if {derpi.%{_target}%.log::*} exists:
            message "&7&l*** &7DERPI log of: &f%{_target}%"
            loop {derpi.%{_target}%.log::*}:
                message loop-value
        else:
            message "&7*** That player has no prior quarries"