# ########################################################################################
# # NEWBIES ##############################################################################
# ########################################################################################

on player join:
    player has permission "gamealition.newbie"
    wait 5 seconds
    message "&2&lWelcome, %arg 1%! &fYou need to introduce yourself at the forums and be accepted by staff into the server. Read the rules at:" to arg 1
    message " &3http://survival.gamealition.com/rules" to arg 1
    message " to find out how!" to arg 1
    
on player teleport:
    # Hacky, but skript has poor region support
    player has permission "gamealition.newbie"
    {guestwarning.%player%} is not set
    set {guestwarning.%player%} to true
    execute console command "/guest-error %player%"
    wait 5 seconds
    delete {guestwarning.%player%}
    
on mine:
    player has permission "gamealition.newbie"
    cancel the event
    execute console command "/guest-error %player%"

on break:
    player has permission "gamealition.newbie"
    cancel the event
    execute console command "/guest-error %player%"

on drop:
    player has permission "gamealition.newbie"
    cancel the event
    execute console command "/guest-error %player%"
    
on pick up:
    player has permission "gamealition.newbie"
    cancel the event
    execute console command "/guest-error %player%"
    
on place:
    player has permission "gamealition.newbie"
    cancel the event
    execute console command "/guest-error %player%"
    
on vehicle destroy:
    player has permission "gamealition.newbie"
    cancel the event
    execute console command "/guest-error %player%"
    
on click:
    player has permission "gamealition.newbie"
    cancel the event
    execute console command "/guest-error %player%"
    
on damage:
    attacker is a player
    attacker has permission "gamealition.newbie"
    cancel the event
    execute console command "/guest-error %attacker%"

# ########################################################################################
# # WORLD ################################################################################
# ########################################################################################
   
every 8 seconds:
    time in "world" is between 06:30 and 17:30
    remove 4 seconds from "world"'s time

# Bed signs
on rightclick on a sign:
    line 1 contains "Bed"
    line 1 contains "["
    line 1 contains "]"
    if player's world is not "world_the_end":
        player's world is not "resource_the_end"
        exit trigger
    
    if bed of player exists:
        teleport player to player's bed
    else:
        execute console command "/mv tp %player% world"

# Horse immunity
on damage:
    victim is horse or donkey or mule
    damage cause is suffocation
    cancel the event

# Block TNT crafting
on crafting of tnt:
    message "&7*** Crafting of TNT is disabled on this server"
    cancel the event

# ########################################################################################
# # COMMAND OVERRIDES ####################################################################
# ########################################################################################

# Multiverse's spawn points are buggy, so we rewire them to anchors
on command:
    command is "mvss" or "mvsetspawn"
    make player execute command "/mv anchor %player's world%"
    make player execute command "/setworldspawn"
    cancel the event

on command:
    command is "mv" or "mvm"
    arguments is "set spawn" or "setspawn"
    make player execute command "/mv anchor %player's world%"
    make player execute command "/setworldspawn"
    cancel the event

on command:
    command is "mvs" or "mvspawn"
    message "&7*** Teleporting you to spawn point of &f%player's world%"
    execute console command "/mvtp %player% a:%player's world%"
    cancel the event

# ########################################################################################
# # SERVER ###############################################################################
# ########################################################################################

# Daily restarts & PRISM purges
# Very hacky, but Skript is limited with IRL date/time...
every minute:
    if "%now%" contains "06:00":
        # Some of PRISM's purge rules seem to be broken, so we manually purge noisy events here
        execute console command "/pr delete a:kill p:fire_tick e:skeleton e:zombie"
        execute console command "/pr delete a:kill p:fire_tick e:guardian"
    else if "%now%" contains "07:55":
        broadcast "&l*** &7Server will perform its daily restart in &ffive minutes&7."
    else if "%now%" contains "07:59":
        broadcast "&l*** &7Server will perform its daily restart in &fone minute&7. &f&lPlease log off now."
    else if "%now%" contains "08:00":
        execute console command "/stop"

# Keeps a database of player UUIDs
on login:
    set {uuid.%name of player%} to UUID of player
    set {uuid.%uuid of player%} to name of player